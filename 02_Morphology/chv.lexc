Multichar_Symbols

%<n%>                ! Имя существительное, Noun
%<pl%>		     ! Множественное число, Plural
%<nom%>              ! Именительный падеж, Nominative
%<gen%>		     ! 			, Genitive
%<ins%>              ! Творительный падеж, Instrumental
%<der_лӑх%>	     ! Derivational Suffix _лӑх

%{A%}                ! Архифонема [а] или [е], Archiphoneme [a] or [e]
%{Ă%}                ! Архифонема [ӑ] или [ӗ] или 0, Archiphoneme [ӑ] or [ӗ] or 0
%{м%}                ! Архифонема [м] или 0, Archiphoneme [м] or 0
%{ъ%}		     ! Forces back harmony in aberrant loans

%>                   ! Граница морфемы, Morpheme boundary



LEXICON Root

Nouns;

LEXICON CASES

%<nom%>:%>       # ;
%<gen%>:%>%{Ă%}н # ;
%<ins%>:%>п%{A%} # ;


LEXICON PLURAL

	         CASES ;
%<pl%>:%>се%{м%} CASES ;

LEXICON SUBST 

PLURAL ;

LEXICON DER-N

%<der_лӑх%>:%>л%{Ă%}х SUBST "weight:1.0" ;

LEXICON N
%<n%>: PLURAL ;
%<n%>: SUBST ;
%<n%>: DER-N ;

LEXICON Nouns

урам:урам N ;         ! "улица", "street"
пахча:пахча N ;       ! "сад", "garden"
хула:хула N ;         ! "город", "city"
канаш:канаш N ;       ! "совет", "council"
тӗс:тӗс N ;           ! "вид", "aspect"
патша:патша N ;	      ! "царь", "tsar"
куҫ:куҫ N ;   	      ! "глаз", "eye"
патшалӑх:патшалӑх N ; ! "государство", "state"
специалист:специалист%{ъ%} N ; ! "специалист", "specialist"
